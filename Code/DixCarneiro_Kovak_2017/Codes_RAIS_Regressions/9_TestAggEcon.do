********************************************************************************
* Dix-Carneiro and Kovak AER replication files
* Generates results in Table 7
* Test for Agglomeration Economies
********************************************************************************

********************************************************************************
********************************************************************************

clear

set more off

set matsize 11000

* global root       "C:/Users/rd123/Dropbox/DixCarneiroKovakRodriguez/ReplicationFiles/"

global data1      "${root}Data/"
global data2      "${root}Data_Other/"
global output     "${root}Results/AgglomerationEconomies/"
global employment "${root}ProcessedData_RAIS/RegionalEmployment/"
global earnings   "${root}ProcessedData_RAIS/RegionalEarnPremia/"

********************************************************************************
********************************************************************************

* Make sure mmc variables across datasets generated by Brian Kovak are 
* transformed to string + rename variables whenever necessary

use ${data1}tariff_chg_kume_subsibge, clear
	sort subsibge
	merge 1:1 subsibge using ${data2}subsibge_to_subsibge_rais
	keep if _merge == 3
	keep subs_ibge dlnonetariff_1990_1995
	rename dlnonetariff_1990_1995 dln_price
	sort subs_ibge
save ${data1}dln_price, replace

use ${data1}rtc_kume
tostring mmc, replace
sort mmc
save ${data1}rtc_kume, replace

use ${data2}mmc_1991_2010_to_c_mesoreg
tostring mmc, replace
rename c_mesoreg mesoreg
sort mmc
save ${data2}rais_mmc_to_mesoreg, replace

********************************************************************************
*******************************************************************************

* Obtain the same sample of regions as in the main specification in the paper
use ${earnings}mmcEarnPremia_main_1986_2010_wide, clear

forvalues year = 1986(1)2010{

	drop if coeff_rem_dez`year' == .

}

* List of regions kept -- we will restrict to those regions when we estimate equation (20)
keep mmc 
duplicates drop
gen use = 1
sort mmc
save ${data1}mmc_use, replace

********************************************************************************
********************************************************************************

use ${employment}mmcEmployment_bysector_1986_2010, clear

sort mmc subs_ibge year
reshape wide emp_dez, i(mmc subs_ibge) j(year)

sort mmc 
merge m:1 mmc using ${data1}rtc_kume, keepusing(rtc_kume_t_notheta_1990_1995)
drop _merge

sort mmc
merge m:1 mmc using ${data2}rais_mmc_to_mesoreg
drop _merge

sort mmc
merge m:1 subs_ibge using ${data1}dln_price
drop _merge

gen state = substr(mmc,1,2)

* Dropping "23014" as it has 0 observations in 1991 and "13007" which is
* Zona Franca de Manaus
drop if trim(mmc) == "23014" | trim(mmc) == "13007"

gen rtr_kume_main = -rtc_kume_t_notheta_1990_1995

* List of regions kept 
sort mmc
merge m:1 mmc using ${data1}mmc_use
drop _merge
keep if use == 1

erase ${data1}mmc_use.dta

********************************************************************************
********************************************************************************

* Aggregate individual NT sectors into a single sector

* Manufacturing/Primary/NT jobs
gen Manuf = 0
replace Manuf = 1 if subs_ibge == "4506" | subs_ibge == "4507" | subs_ibge == "4508" | ///
					 subs_ibge == "4509" | subs_ibge == "4510" | subs_ibge == "4511" | ///
					 subs_ibge == "4512" | subs_ibge == "4513" | subs_ibge == "4514" | ///
					 subs_ibge == "4515" | subs_ibge == "4516" | subs_ibge == "4517"
* Primary jobs (Agriculture/Mining)
gen Primary = 0
replace Primary = 1 if subs_ibge == "1101" | subs_ibge == "4405" 
* Traded goods jobs (Manufacturing + Primary)
gen Traded = 0
replace Traded = 1 if Manuf == 1 | Primary == 1
gen NT = 1 - Traded	

sort mmc NT
by mmc NT: egen emp_T_NT1986 = total(emp_dez1986)
by mmc NT: egen emp_T_NT1990 = total(emp_dez1990)
by mmc NT: egen emp_T_NT1991 = total(emp_dez1991)
by mmc NT: egen emp_T_NT2010 = total(emp_dez2010)

replace emp_dez1986 = emp_T_NT1986 if NT == 1
replace emp_dez1990 = emp_T_NT1990 if NT == 1
replace emp_dez1991 = emp_T_NT1991 if NT == 1
replace emp_dez2010 = emp_T_NT2010 if NT == 1

replace subs_ibge = "NT" if NT == 1

keep mmc mesoreg emp_dez1986 emp_dez1990 emp_dez1991 emp_dez2010 rtr_kume_main subs_ibge state NT Manuf Primary dln_price
duplicates drop

********************************************************************************
********************************************************************************

tab subs_ibge, gen(indFE)

gen pre_trend = log(emp_dez1990) - log(emp_dez1986)
drop if pre_trend == .

replace dln_price = -rtr_kume_main if NT == 1

qui tab state, gen(state)

cap drop LHS
gen LHS = log(emp_dez2010) - log(emp_dez1991)

set more off

reg LHS rtr_kume_main dln_price, cluster(mesoreg)
outreg2 rtr_kume_main using ${output}Table7, excel bdec(4) replace

reg LHS rtr_kume_main dln_price state2-state26, cluster(mesoreg)
outreg2 rtr_kume_main using ${output}Table7, excel bdec(4) append

reg LHS rtr_kume_main dln_price pre_trend state2-state26, cluster(mesoreg)
outreg2 rtr_kume_main pre_trend using ${output}Table7, excel bdec(4) append

reg LHS rtr_kume_main indFE1-indFE15 pre_trend state2-state26, cluster(mesoreg)
outreg2 rtr_kume_main pre_trend using ${output}Table7, excel bdec(4) append

reg LHS  rtr_kume_main dln_price pre_trend state2-state26 if NT == 0, cluster(mesoreg)
outreg2 rtr_kume_main pre_trend using ${output}Table7, excel bdec(4) append

reg LHS  rtr_kume_main indFE1-indFE15 pre_trend state2-state26 if NT == 0, cluster(mesoreg)
outreg2 rtr_kume_main pre_trend using ${output}Table7, excel bdec(4) append

********************************************************************************
********************************************************************************

erase ${data1}dln_price.dta
erase ${data2}rais_mmc_to_mesoreg.dta
